plugins {
    id "idea"
    id "jacoco"
    id "maven-publish"
    id "com.jfrog.bintray" version "1.8.5"
    id "org.jetbrains.kotlin.jvm" version "1.5.21"
    id "org.jetbrains.kotlin.plugin.jpa" version "1.5.21"
    id "org.jetbrains.kotlin.plugin.spring" version "1.5.21"
    id "org.springframework.boot" version "2.5.0"
    id "io.spring.dependency-management" version "1.0.11.RELEASE"
    id "org.springframework.cloud.contract" version "3.0.3"
}

[compileKotlin, compileTestKotlin]*.kotlinOptions *.jvmTarget = "11"

ext {
    springCloudVersion = "3.0.3"
    gsonVersion = "2.8.7"
    guavaVersion = "30.1.1-jre"
    openApiVersion = "1.5.10"
    commonsLangVersion = "3.12.0"
    hibernateTypesVersion = "2.10.4"
    postgresContainerVersion = "1.15.2"
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-contract-dependencies:$springCloudVersion"
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        xml.destination file("${buildDir}/reports/jacoco/report.xml")
        html.enabled false
        csv.enabled false
    }
}

repositories {
    mavenCentral()
}

publishing {
    publications {
        stubs(MavenPublication) {
            artifact verifierStubsJar
            artifactId "warehouse"
        }
    }
}

bintray {
    user = System.getenv("BINTRAY_USER")
    key = System.getenv("BINTRAY_TOKEN")
    publications = ["stubs"]
    publish = true
    pkg {
        repo = "scc-microservices"
        name = "warehouse"
    }
}

contracts {
    baseClassMappings {
        baseClassMapping ".*items.*", "ru.romanow.scc.warehouse.web.BaseItemsControllerTest"
        baseClassMapping ".*state.*", "ru.romanow.scc.warehouse.web.BaseStateControllerTest"
        baseClassMapping ".*checkout.*", "ru.romanow.scc.warehouse.web.BaseCheckoutControllerTest"
        baseClassMapping ".*takeItems.*", "ru.romanow.scc.warehouse.web.BaseTakeItemControllerTest"
    }
    basePackageForTests = "ru.romanow.scc.warehouse.web"
    testFramework = "JUNIT5"
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.boot:spring-boot-starter-logging"

    implementation "org.postgresql:postgresql"
    implementation "org.flywaydb:flyway-core"
    implementation "com.vladmihalcea:hibernate-types-52:$hibernateTypesVersion"

    implementation "org.springdoc:springdoc-openapi-ui:$openApiVersion"
    implementation "org.springdoc:springdoc-openapi-kotlin:$openApiVersion"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "org.jetbrains.kotlin:kotlin-reflect"
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin"

    implementation "com.google.guava:guava:$guavaVersion"
    implementation "com.google.code.gson:gson:$gsonVersion"
    implementation "org.apache.commons:commons-lang3:$commonsLangVersion"
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

    testImplementation "org.mockito:mockito-inline"
    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "org.springframework.cloud:spring-cloud-contract-spec"
    testImplementation "org.springframework.cloud:spring-cloud-contract-spec-kotlin"
    testImplementation "org.springframework.cloud:spring-cloud-starter-contract-verifier"
    testImplementation "org.testcontainers:postgresql:$postgresContainerVersion"
}

test {
    useJUnitPlatform()
}

contractTest {
    useJUnitPlatform()
}

bootJar {
    archiveFileName = "${project.name}.jar"
    layered {
        enabled = true
    }
}